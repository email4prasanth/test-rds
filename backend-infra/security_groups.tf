# Security Group for hubino Resources Access
resource "aws_security_group" "hubino_sg" {
  name        = "${terraform.workspace}-${local.project_name.name}-sg"
  description = "Security group for hubino instances"
  vpc_id      = aws_vpc.hubino-vpc.id

  dynamic "ingress" {
    for_each = [for rule in local.sg : rule if rule.type == "ingress"]
    content {
      description = ingress.value.description
      from_port   = ingress.value.from_port
      to_port     = ingress.value.to_port
      protocol    = ingress.value.protocol
      cidr_blocks = ingress.value.cidr_blocks
    }
  }

  dynamic "egress" {
    for_each = [for rule in local.sg : rule if rule.type == "egress"]
    content {
      description = egress.value.description
      from_port   = egress.value.from_port
      to_port     = egress.value.to_port
      protocol    = egress.value.protocol
      cidr_blocks = egress.value.cidr_blocks
    }
  }

  tags = {
    Name = "${local.project_name.name}-sg-${terraform.workspace}"
  }
}

# Security Group for RDS PostgreSQL Access
resource "aws_security_group" "rds" {
  name        = "${terraform.workspace}-rds-sg"
  description = "Restricted access to PostgreSQL"
  vpc_id      = aws_vpc.hubino-vpc.id

  ingress {
    from_port       = 5432
    to_port         = 5432
    protocol        = "tcp"
    # cidr_blocks     = ["0.0.0.0/0"]
    security_groups = [aws_security_group.lambda_sg.id]
  }

  ingress {
    description     = "Allow PostgreSQL from my IP"  # Add your IP here
    from_port       = 5432
    to_port         = 5432
    protocol        = "tcp"
    cidr_blocks     = ["117.213.147.119/32"]  # e.g., "123.45.67.89/32"
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = local.tags
}

# Security Group for Lambda Function
resource "aws_security_group" "lambda_sg" {
  name        = "${terraform.workspace}-lambda-sg"
  description = "Lambda access to RDS and internet"
  vpc_id      = aws_vpc.hubino-vpc.id

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = merge(local.tags, {
    Name = "${local.project_name.name}-lambda-sg-${terraform.workspace}"
  })
}